buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.6'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.7.0-alpha1"
group= "com.cout970.magneticraft"
archivesBaseName = "magneticraft"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

curseforge {
    apiKey = project.curseForgeApiKey
    project {
        id = '224808'
        mainArtifact(jar) {
            displayName = 'Magneticraft ' + version
        }
        releaseType = 'release'
        changelog = file('./changelogs/' + version + '.txt')
        if (version.contains('beta')) {
            releaseType = 'beta'
        }
        if (version.contains('alpha')) {
            releaseType = 'alpha'
        }
        relations {
            requiredLibrary 'mcmultipart'
        }
    }
}

minecraft {
    version = "1.8.9-11.15.0.1693"
    runDir = "eclipse"

    replace "@VERSION@", project.version
    replaceIn "Magneticraft.java"
    mappings = "stable_20"
}

repositories {
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name = "tehnut"
        url = "http://tehnut.info/maven/"
    }
    maven {
        name = "amadornes"
        url = "http://maven.amadornes.com/"
    }
}

dependencies {
    deobfCompile "MCMultiPart:MCMultiPart:1.0.4:universal"
    //compile "mcp.mobius.waila:Waila:1.5.10_1.7.10"
    //compile "info.amerifrance.guideapi:Guide-API:1.7.10-1.0.1-20:deobf"
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
                exclude 'mgc_at.cfg'
            }
        }

jar {
    manifest {
        attributes 'FMLAT': 'magneticraft_at.cfg'
    }
}
task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives devJar
}
apply plugin: 'idea'
idea.module.inheritOutputDirs = true
